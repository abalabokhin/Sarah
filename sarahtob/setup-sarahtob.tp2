BACKUP ~weidu_external/sarahtob/backup~
SUPPORT ~https://www.gibberlings3.net/forums/forum/135-sarah/~

VERSION ~v8~
README ~SarahToB/readme-sarahtob.html~

//MODDER

ALWAYS

  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END

  OUTER_SET enhanced_edition = 0  
  OUTER_SET game_contains_tob = 1  
  OUTER_SPRINT ~tra_location~ ~sarahtob/translations~

  ACTION_IF GAME_IS ~bg2ee eet~ BEGIN 

    OUTER_SET enhanced_edition = 1   
    OUTER_SPRINT ~tra_location~ ~weidu_external/sarahtob/translations~
    
    ACTION_DEFINE_ARRAY fl#noconvert BEGIN END
    ACTION_DEFINE_ARRAY fl#reload BEGIN setup END
    LAF HANDLE_CHARSETS
      INT_VAR
        infer_charsets = 1
      STR_VAR
        tra_path = ~sarahtob/translations~
        out_path = ~weidu_external/sarahtob/translations~
        noconvert_array = fl#noconvert
        reload_array = fl#reload
    END
    
  END ELSE  
  
  ACTION_IF GAME_IS ~soa~ BEGIN  
    OUTER_SET game_contains_tob = 0
  END 
  
END

// Translations
AUTO_TRA ~%tra_location%/%s~


LANGUAGE ~English~
         ~english~
         ~sarahtob/translations/english/setup.tra~
LANGUAGE ~French (by Le Marquis from Les d'Oghmatiques)~
         ~french~
         ~sarahtob/translations/english/setup.tra~
         ~sarahtob/translations/french/setup.tra~
LANGUAGE ~Russian (by tipun)~
         ~russian~
         ~%tra_location%/english/setup.tra~
         ~%tra_location%/russian/setup.tra~
         
BEGIN @60 DESIGNATED 0
SUBCOMPONENT @0
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee bgt eet~ @62
LABEL ~c#_sarah_original_audio~

INCLUDE ~sarahtob/lib/sarah_main.tpa~
LAF HANDLE_AUDIO STR_VAR audio_path = ~sarahtob/audio/common~ END
LAF HANDLE_AUDIO STR_VAR audio_path = ~sarahtob/audio/original~ END

BEGIN @61 DESIGNATED 1
SUBCOMPONENT @0
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee bgt eet~ @62
LABEL ~c#_sarah_revised_audio~

INCLUDE ~sarahtob/lib/sarah_main.tpa~
LAF HANDLE_AUDIO STR_VAR audio_path = ~sarahtob/audio/common~ END
LAF HANDLE_AUDIO STR_VAR audio_path = ~sarahtob/audio/halmylyseas~ END
